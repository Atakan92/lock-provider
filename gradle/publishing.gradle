apply plugin: 'maven'
apply plugin: 'maven-publish'

def releaseRepoUrl = project.findProperty('mavenReleaseUrl')
        ?: System.getenv('MAVEN_RELEASE_URL')
        ?: 'Not Specified'

def snapshotRepoUrl = project.findProperty('mavenSnapshotUrl')
        ?: System.getenv('MAVEN_SNAPSHOT_URL')
        ?: 'Not Specified'

def repoUser = project.findProperty('mavenUsername')
        ?: System.getenv('MAVEN_USERNAME')
        ?: 'Not Specified'

def repoPass = project.findProperty('mavenPassword')
        ?: System.getenv('MAVEN_PASSWORD')
        ?: 'Not Specified'

def isSnapshot = Boolean.parseBoolean(System.getenv("SNAPSHOT_RELEASE"))
def repoUrl = isSnapshot ? snapshotRepoUrl : releaseRepoUrl

println("Maven URL: ${repoUrl}, Maven Username: ${repoUser}")

publishing {
    repositories {
        maven {
            url "${repoUrl}"
            credentials {
                username "${repoUser}"
                password "${repoPass}"
            }
        }
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}
